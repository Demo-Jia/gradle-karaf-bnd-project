apply plugin: 'base'
apply plugin: 'maven-publish'
apply from: "./libraries.gradle"

buildscript {
  repositories {
     mavenCentral()
     mavenLocal()
     jcenter()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.0.0'
  }
}
allprojects{
	group 'net.sf.greeting'
}
def bundleProjects() { 
  return subprojects.findAll { new File(it.projectDir, "bnd.bnd").exists() }
} 

configure(bundleProjects()) {       
    apply plugin: 'biz.aQute.bnd.builder'
    apply plugin: 'maven-publish'
 
    repositories {
       mavenCentral()
       mavenLocal()
    }
  
    sourceCompatibility = 1.8
    
    dependencies {
        testCompile( libraries.junit )
        compile (libraries.bnd )
    }
    
    
	task sourcesJar(type: Jar, dependsOn: classes) {
	    from sourceSets.main.allSource
	}
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
	    from javadoc.destinationDir
	}
	
	task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task install(type: Task, dependsOn: publishToMavenLocal)

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

	            artifact sourceJar {
	                classifier "sources"
	            }
	            artifact javadocJar {
                   classifier "javadoc"
                }
                
                pom.withXml {
       				/*
	                  asNode().children().first().parent()
	                      .appendNode('repositories')
	                        .appendNode('repository')
	                          .appendNode('id', 'central').parent()
	                          .appendNode('url', ' http://repo1.maven.org/maven2/')
	                */
	                
       				asNode().children().first() + {
       					
       					// See https://discuss.gradle.org/t/some-elements-are-removed-when-generate-a-pom-xml-using-the-maven-publish-plugin/2073/2
       					resolveStrategy = Closure.DELEGATE_FIRST
       					
						name project.name
						description 'Sample bnd gradle integration'
						url 'https://github.com/antoniomaria/gradle-bnd-project'
						developers {
                	        developer {
            	                id 'antoniomaria'
        	                    name 'Antonio Maria Sanchez Berrocal'
    	                    }
	                    }
					}
       				// TEMPORARY : currently Gradle Publishing feature is exporting dependencies as 'runtime' scope,
	                //      rather than 'compile'; fix that.
    	            asNode().dependencies[0].dependency.each {
        	            it.scope[0].value = 'compile'
            	    }
      			}
            }
        }
    }
}


def nonJavaProjects(){
 return subprojects - bundleProjects();
}

configure(nonJavaProjects()){
  	apply plugin: 'base'
	apply plugin: 'maven-publish'
	
	repositories {
		mavenCentral()
		mavenLocal()
	}
	
    task install(type: Task, dependsOn: publishToMavenLocal)
}

task showVersion << {
	println ("Project version = $version")
}

build.dependsOn showVersion

// publishToMavenLocal.dependsOn build

